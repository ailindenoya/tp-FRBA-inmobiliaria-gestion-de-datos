USE [GD2C2023]
GO

CREATE SCHEMA CUATRO_HISPANOS;
GO

--CREACIONES DE TABLAS

CREATE TABLE CUATRO_HISPANOS.PAGO_ALQUILER(
    NRO_PAGO_ALQUILER NUMERIC(18,0) PRIMARY KEY,
    FECHA DATETIME NOT NULL,
    NRO_PERIODO NUMERIC(18,0) ,
    DESCRIPCION_PERIODO NVARCHAR(100) NULL,
    FECHA_INICIO_PAGO_ALQUILER DATETIME NULL,
    FECHA_FIN_PAGO_ALQUILER DATETIME NULL,
	FECHA_VENC_ALQUILER DATETIME NOT NULL,
    IMPORTE_PAGO_ALQUILER NUMERIC(18,2) NOT NULL,
    NRO_ALQUILER NUMERIC(18,0) NOT NULL,
    NRO_MEDIO_PAGO NUMERIC(18,0) NOT NULL
);

CREATE TABLE CUATRO_HISPANOS.ALQUILER(
    NRO_ALQUILER  NUMERIC(18,0) PRIMARY KEY IDENTITY (1,1),
    FECHA_INICIO DATETIME NULL,
    FECHA_FIN DATETIME NULL,
    DEPOSITO_ALQUILER NUMERIC(18,2) NULL,
    COMISION_ALQUILER NUMERIC(18,2) NOT NULL,
    GASTOS_AVERIGUACIONES NUMERIC(18,2) NULL,
    CANT_PERIODOS NUMERIC(18,0) NULL,
    NRO_INQUILINO NUMERIC(18,0) NOT NULL,
    NRO_ANUNCIO  NUMERIC(19,0) NOT NULL,
    NRO_ESTADO_ALQUILER NUMERIC(18,0) NOT NULL,
	ALQUILER_CODIGO NUMERIC(18,0) NOT NULL
);


CREATE TABLE CUATRO_HISPANOS.INMUEBLE(

    NRO_INMUEBLE NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    NOMBRE_INMUEBLE NVARCHAR(100) NOT NULL,
    DESCRIPCION_INMUEBLE NVARCHAR(100) NULL,
    DIRECCION_INMUEBLE NVARCHAR(100) NULL,
    ANTIGUEDAD_INMUEBLE NUMERIC(18,0) NULL,
    SUPERFICIE_INMUEBLE NUMERIC(18,0) NOT NULL,
    EXPENSAS_INMUEBLE NUMERIC(18,2) NULL,
    NRO_PROPIETARIO NUMERIC(18,0) NOT NULL,
    NRO_UBICACION NUMERIC(18, 0) NOT NULL,
    NRO_ORIENTACION NUMERIC(18,0) NOT NULL,
    NRO_DISPOSICION  NUMERIC(18,0) NOT NULL,
    NRO_TIPO  NUMERIC(18,0) NOT NULL,
    NRO_AMBIENTE NUMERIC(18, 0) NULL,
    NRO_ESTADO  NUMERIC(18,0) NOT NULL,
	INMUEBLE_CODIGO NUMERIC(18,0) NOT NULL,
);

CREATE TABLE CUATRO_HISPANOS.ANUNCIO(
    NRO_ANUNCIO  NUMERIC(19,0) PRIMARY KEY IDENTITY (1,1),
    FECHA_PUBLICACION_ANUNCIO DATETIME  NULL,
    FECHA_FINALIZACION_ANUNCIO DATETIME NULL,
    PRECIO_ANUNCIO NUMERIC(18,2) NOT NULL,
    COSTO_ANUNCIO NUMERIC(18,2) NULL,
    NRO_AGENTE NUMERIC(18,0) NOT NULL,
    NRO_INMUEBLE  NUMERIC(18,0) NOT NULL,
    NRO_OPERACION NUMERIC(18,0) NOT NULL,
    NRO_ESTADO_ANUNCIO NUMERIC(18,0) NOT NULL,
    NRO_TIPO_PERIODO NUMERIC(18,0) NOT NULL,
    TIPO_MONEDA NUMERIC(18,0) NOT NULL,
	ANUNCIO_CODIGO NUMERIC(19,0) NOT NULL  
);


CREATE TABLE CUATRO_HISPANOS.DISPOSICION(
    NRO_DISPOSICION  NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    DISPOSICION NVARCHAR(100) NOT NULL UNIQUE
);
CREATE TABLE CUATRO_HISPANOS.TIPO_INMUEBLE(
    NRO_TIPO  NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    TIPO_INMUEBLE NVARCHAR(100) NOT NULL UNIQUE
);

CREATE TABLE CUATRO_HISPANOS.ESTADO_INMUEBLE(
    NRO_ESTADO  NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    ESTADO_INMUEBLE NVARCHAR(100) NOT NULL UNIQUE
);



CREATE TABLE CUATRO_HISPANOS.VENTA(
    NRO_VENTA NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    NRO_COMPRADOR NUMERIC(18,0) NOT NULL,
    MONEDA NUMERIC(18,0) NOT NULL,
    NRO_ANUNCIO NUMERIC(19, 0) NOT NULL,
    PRECIO_VENTA NUMERIC(18,2) NOT NULL,
    COMISION_VENTA NUMERIC(18,2) NOT NULL,
    FECHA_VENTA DATETIME NOT NULL,
	CODIGO_VENTA NUMERIC(18,0) NOT NULL
);


CREATE TABLE CUATRO_HISPANOS.MONEDA(
    TIPO_MONEDA NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    MONEDA NVARCHAR(100) NOT NULL UNIQUE
);


CREATE TABLE CUATRO_HISPANOS.PAGO_VENTA(
    NRO_PAGO_VENTA NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    NRO_VENTA NUMERIC(18, 0) NOT NULL,
    MEDIO_PAGO NUMERIC(18,0) NOT NULL, 
    MONEDA NUMERIC(18, 0) NOT NULL,
    IMPORTE_PAGO_VENTA NUMERIC(18,2) NULL,
    COTIZACION_PAGO_VENTA NUMERIC(18,2) NULL
);


CREATE TABLE CUATRO_HISPANOS.COMPRADOR(
    NRO_COMPRADOR NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    COMPRADOR_NOMBRE NVARCHAR(100) NULL,
    COMPRADOR_APELLIDO NVARCHAR(100) NULL,
    COMPRADOR_DNI NUMERIC(18, 0) NULL,
    COMPRADOR_FECHA_REGISTRO DATETIME NULL,
    COMPRADOR_TELEFONO NUMERIC(18, 0) NULL,
    COMPRADOR_FECHA_NAC DATETIME NULL,
    COMPRADOR_MAIL NVARCHAR(100) NULL
);


CREATE TABLE CUATRO_HISPANOS.DETALLE_IMPORTE(
    NRO_DETALLE NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    DETALLE_PERIODO_INICIO NUMERIC(18,0) NULL,
    DETALLE_PERIODO_FIN NUMERIC(18,0) NULL,
    DETALLE_PRECIO NUMERIC(18,2) NULL,
    NRO_ALQUILER NUMERIC (18,0) NULL
);




CREATE TABLE CUATRO_HISPANOS.MEDIO_PAGO(
    NRO_MEDIO_PAGO NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    MEDIO_PAGO NVARCHAR(100) NOT NULL UNIQUE
);


CREATE TABLE CUATRO_HISPANOS.ESTADO_ALQUILER(
    NRO_ESTADO_ALQUILER NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    ESTADO_ALQUILER NVARCHAR(100) NOT NULL UNIQUE
);


CREATE TABLE CUATRO_HISPANOS.ORIENTACION(
    NRO_ORIENTACION NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    ORIENTACION NVARCHAR(100) NOT NULL UNIQUE
);

CREATE TABLE CUATRO_HISPANOS.TIPO_PERIODO(
    NRO_TIPO_PERIODO NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    TIPO_PERIODO NVARCHAR(100) NOT NULL UNIQUE
);

CREATE TABLE CUATRO_HISPANOS.PROPIETARIO(
    NRO_PROPIETARIO NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    PROPIETARIO_NOMBRE NVARCHAR(100) NULL,
    PROPIETARIO_APELLIDO NVARCHAR(100) NULL,
    PROPIETARIO_DNI NUMERIC(18, 0) NULL,
    PROPIETARIO_FECHA_REGISTRO DATETIME NULL,
    PROPIETARIO_TELEFONO NUMERIC(18, 0) NULL,
    PROPIETARIO_MAIL NVARCHAR(255) NULL,
    PROPIETARIO_FECHA_NAC DATETIME NULL
);



CREATE TABLE CUATRO_HISPANOS.INQUILINO(
    NRO_INQUILINO NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    INQUILINO_NOMBRE NVARCHAR(100) NULL,
    INQUILINO_APELLIDO NVARCHAR(100) NULL,
    INQUILINO_DNI NUMERIC(18, 0) NULL,
    INQUILINO_FECHA_REGISTRO DATETIME NULL,
    INQUILINO_TELEFONO NUMERIC(18, 0) NULL,
    INQUILINO_MAIL NVARCHAR(100) NULL,
    INQUILINO_FECHA_NAC DATETIME NULL
);


CREATE TABLE CUATRO_HISPANOS.AGENTE(
    NRO_AGENTE  NUMERIC(18,0)  PRIMARY KEY IDENTITY(1,1),
    NRO_SUCURSAL NUMERIC(18,0) NOT NULL,
    NOMBRE_AGENTE NVARCHAR(100) NULL,
    APELLIDO_AGENTE NVARCHAR(100) NULL,
    DNI_AGENTE NUMERIC(18,0) NULL,
    FECHA_REGISTRO_AGENTE DATETIME NULL,
    TELEFONO NUMERIC(18,0) NULL,
    MAIL NVARCHAR(100) NULL,
    FECHA_NAC_AGENTE DATETIME NULL
);


CREATE TABLE CUATRO_HISPANOS.SUCURSAL(
    NRO_SUCURSAL NUMERIC(18, 0) PRIMARY KEY,
    SUCURSAL_NOMBRE NVARCHAR(100) NULL,
    SUCURSAL_DIRECCION NVARCHAR(100) NULL,
    SUCURSAL_TELEFONO NVARCHAR(100) NULL,
    NRO_LOCALIDAD NUMERIC(18,0) NOT NULL,
);


CREATE TABLE CUATRO_HISPANOS.CARACTERISTICA(
    NRO_CARACTERISTICA NUMERIC(18, 0) PRIMARY KEY IDENTITY(1,1),
    TIPO_CARACTERISTICA NVARCHAR(100) NOT NULL UNIQUE
);

CREATE TABLE CUATRO_HISPANOS.CARACTERISTICA_X_INMUEBLE(
	NRO_CARACTERISTICA_X_INMUEBLE NUMERIC(18,0) PRIMARY KEY IDENTITY(1,1),
    NRO_CARACTERISTICA NUMERIC(18,0),
    NRO_INMUEBLE NUMERIC(18,0)
);



CREATE TABLE CUATRO_HISPANOS.AMBIENTE(
    NRO_AMBIENTE NUMERIC(18, 0) PRIMARY KEY IDENTITY(1,1),
    EXTRA NVARCHAR(100) NULL,
    CANTIDAD NUMERIC(18, 0) NOT NULL
);

CREATE TABLE CUATRO_HISPANOS.UBICACION(
    NRO_UBICACION NUMERIC(18, 0) PRIMARY KEY IDENTITY(1,1),
    NRO_BARRIO NUMERIC(18,0) NOT NULL,
    NRO_LOCALIDAD NUMERIC(18,0) NOT NULL,
    NRO_PROVINCIA NUMERIC(18,0) NOT NULL
);

CREATE TABLE CUATRO_HISPANOS.BARRIO(
    NRO_BARRIO NUMERIC(18, 0) PRIMARY KEY IDENTITY(1,1),
    BARRIO NVARCHAR(100) NOT NULL,
    NRO_LOCALIDAD NUMERIC(18,0) NOT NULL 
);


CREATE TABLE CUATRO_HISPANOS.LOCALIDAD(
    NRO_LOCALIDAD NUMERIC(18, 0) PRIMARY KEY IDENTITY(1,1),
    NRO_PROVINCIA NUMERIC(18,0) NOT NULL,
    LOCALIDAD NVARCHAR(100) NOT NULL
);



CREATE TABLE CUATRO_HISPANOS.PROVINCIA(
    NRO_PROVINCIA NUMERIC(18, 0) PRIMARY KEY IDENTITY(1,1),
    PROVINCIA NVARCHAR(100) NOT NULL
);


CREATE TABLE CUATRO_HISPANOS.ESTADO_ANUNCIO(
    NRO_ESTADO_ANUNCIO NUMERIC(18,0)  PRIMARY KEY IDENTITY(1,1),
    ESTADO_ANUNCIO NVARCHAR(100) NOT NULL UNIQUE
);

CREATE TABLE CUATRO_HISPANOS.TIPO_OPERACION(
    NRO_OPERACION NUMERIC(18,0)  PRIMARY KEY IDENTITY(1,1),
    TIPO_OPERACION NVARCHAR(100) NOT NULL UNIQUE
);



-- INICIALIZACION FKS

ALTER TABLE CUATRO_HISPANOS.ALQUILER
    ADD FOREIGN KEY (NRO_INQUILINO) REFERENCES CUATRO_HISPANOS.INQUILINO(NRO_INQUILINO),
        FOREIGN KEY (NRO_ANUNCIO) REFERENCES CUATRO_HISPANOS.ANUNCIO(NRO_ANUNCIO),    
        FOREIGN KEY (NRO_ESTADO_ALQUILER) REFERENCES CUATRO_HISPANOS.ESTADO_ALQUILER(NRO_ESTADO_ALQUILER);
GO


ALTER TABLE CUATRO_HISPANOS.ANUNCIO
    ADD FOREIGN KEY (NRO_AGENTE) REFERENCES CUATRO_HISPANOS.AGENTE(NRO_AGENTE),
        FOREIGN KEY (NRO_INMUEBLE) REFERENCES CUATRO_HISPANOS.INMUEBLE(NRO_INMUEBLE),
        FOREIGN KEY (NRO_OPERACION) REFERENCES CUATRO_HISPANOS.TIPO_OPERACION(NRO_OPERACION),
        FOREIGN KEY (NRO_ESTADO_ANUNCIO) REFERENCES CUATRO_HISPANOS.ESTADO_ANUNCIO(NRO_ESTADO_ANUNCIO),
        FOREIGN KEY (NRO_TIPO_PERIODO) REFERENCES CUATRO_HISPANOS.TIPO_PERIODO(NRO_TIPO_PERIODO),
        FOREIGN KEY (TIPO_MONEDA) REFERENCES CUATRO_HISPANOS.MONEDA(TIPO_MONEDA);
GO 


ALTER TABLE CUATRO_HISPANOS.AGENTE
    ADD FOREIGN KEY (NRO_SUCURSAL) REFERENCES CUATRO_HISPANOS.SUCURSAL(NRO_SUCURSAL);
GO


ALTER TABLE CUATRO_HISPANOS.SUCURSAL         
    ADD FOREIGN KEY (NRO_LOCALIDAD) REFERENCES CUATRO_HISPANOS.LOCALIDAD(NRO_LOCALIDAD);
GO


ALTER TABLE CUATRO_HISPANOS.INMUEBLE
    ADD FOREIGN KEY (NRO_PROPIETARIO) REFERENCES CUATRO_HISPANOS.PROPIETARIO(NRO_PROPIETARIO),
        FOREIGN KEY (NRO_UBICACION) REFERENCES CUATRO_HISPANOS.UBICACION(NRO_UBICACION),
        FOREIGN KEY (NRO_ORIENTACION) REFERENCES CUATRO_HISPANOS.ORIENTACION(NRO_ORIENTACION),
        FOREIGN KEY (NRO_DISPOSICION) REFERENCES CUATRO_HISPANOS.DISPOSICION(NRO_DISPOSICION),
        FOREIGN KEY (NRO_TIPO) REFERENCES CUATRO_HISPANOS.TIPO_INMUEBLE(NRO_TIPO),
        FOREIGN KEY (NRO_AMBIENTE) REFERENCES CUATRO_HISPANOS.AMBIENTE(NRO_AMBIENTE),
        FOREIGN KEY (NRO_ESTADO) REFERENCES CUATRO_HISPANOS.ESTADO_INMUEBLE(NRO_ESTADO);
GO


ALTER TABLE CUATRO_HISPANOS.PAGO_ALQUILER
    ADD FOREIGN KEY (NRO_ALQUILER) REFERENCES CUATRO_HISPANOS.ALQUILER(NRO_ALQUILER),
        FOREIGN KEY (NRO_MEDIO_PAGO) REFERENCES CUATRO_HISPANOS.MEDIO_PAGO(NRO_MEDIO_PAGO);
GO


ALTER TABLE CUATRO_HISPANOS.VENTA
    ADD FOREIGN KEY (NRO_ANUNCIO) REFERENCES CUATRO_HISPANOS.ANUNCIO(NRO_ANUNCIO),
        FOREIGN KEY (NRO_COMPRADOR) REFERENCES CUATRO_HISPANOS.COMPRADOR(NRO_COMPRADOR),
        FOREIGN KEY (MONEDA) REFERENCES CUATRO_HISPANOS.MONEDA(TIPO_MONEDA);
GO


ALTER TABLE CUATRO_HISPANOS.PAGO_VENTA
    ADD FOREIGN KEY (NRO_VENTA) REFERENCES CUATRO_HISPANOS.VENTA(NRO_VENTA),
        FOREIGN KEY (MEDIO_PAGO) REFERENCES CUATRO_HISPANOS.MEDIO_PAGO(NRO_MEDIO_PAGO),
        FOREIGN KEY (MONEDA) REFERENCES CUATRO_HISPANOS.MONEDA(TIPO_MONEDA);
GO


ALTER TABLE CUATRO_HISPANOS.UBICACION
    ADD FOREIGN KEY (NRO_BARRIO) REFERENCES CUATRO_HISPANOS.BARRIO(NRO_BARRIO),
        FOREIGN KEY (NRO_LOCALIDAD) REFERENCES CUATRO_HISPANOS.LOCALIDAD(NRO_LOCALIDAD),
        FOREIGN KEY (NRO_PROVINCIA) REFERENCES CUATRO_HISPANOS.PROVINCIA(NRO_PROVINCIA);
GO


ALTER TABLE CUATRO_HISPANOS.BARRIO
    ADD FOREIGN KEY (NRO_LOCALIDAD) REFERENCES CUATRO_HISPANOS.LOCALIDAD(NRO_LOCALIDAD);
GO


ALTER TABLE CUATRO_HISPANOS.LOCALIDAD
    ADD FOREIGN KEY (NRO_PROVINCIA) REFERENCES CUATRO_HISPANOS.PROVINCIA(NRO_PROVINCIA);
GO


ALTER TABLE CUATRO_HISPANOS.DETALLE_IMPORTE
    ADD FOREIGN KEY (NRO_ALQUILER) REFERENCES CUATRO_HISPANOS.ALQUILER(NRO_ALQUILER);
GO

ALTER TABLE CUATRO_HISPANOS.CARACTERISTICA_X_INMUEBLE
    ADD FOREIGN KEY (NRO_CARACTERISTICA) REFERENCES CUATRO_HISPANOS.CARACTERISTICA(NRO_CARACTERISTICA),
		FOREIGN KEY (NRO_INMUEBLE) REFERENCES CUATRO_HISPANOS.INMUEBLE(NRO_INMUEBLE);
GO



-- FUNCIONES DE RECUPERACION DE PKS


CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_prop(@DNI NUMERIC(18,0), @APELLIDO NVARCHAR(100), @NOMBRE NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_prop NUMERIC(18,0);

SELECT @id_prop = NRO_PROPIETARIO 
FROM CUATRO_HISPANOS.PROPIETARIO as P
WHERE  P.PROPIETARIO_DNI = @DNI 
AND P.PROPIETARIO_APELLIDO = @APELLIDO
AND P.PROPIETARIO_NOMBRE = @NOMBRE;

RETURN @id_prop;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_tipo_inmueble(@TIPO NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_tipo NUMERIC(18,0);

SELECT @id_tipo = NRO_TIPO 
FROM CUATRO_HISPANOS.TIPO_INMUEBLE as T
WHERE  T.TIPO_INMUEBLE = @TIPO 
RETURN @id_tipo;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_disposicion(@DISPOSICION NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_dispo NUMERIC(18,0);

SELECT @id_dispo = NRO_DISPOSICION
FROM CUATRO_HISPANOS.DISPOSICION as D
WHERE  D.DISPOSICION = @DISPOSICION 

RETURN @id_dispo;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_orientacion(@ORIENTACION NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_ori NUMERIC(18,0);

SELECT @id_ori = NRO_ORIENTACION
FROM CUATRO_HISPANOS.ORIENTACION as O
WHERE  O.ORIENTACION = @ORIENTACION  

RETURN @id_ori;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_estadoInmueble(@ESTADO NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_estado NUMERIC(18,0);

SELECT @id_estado = NRO_ESTADO
FROM CUATRO_HISPANOS.ESTADO_INMUEBLE as E
WHERE  E.ESTADO_INMUEBLE = @ESTADO

RETURN @id_estado;
END
GO


CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_ambiente(@AMBIENTEMAESTRA NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN


DECLARE @ambiente NUMERIC(18,0);
DECLARE @cant_ambientes NUMERIC(18,0);
DECLARE @extra NVARCHAR(100)
	
IF CHARINDEX('Monoambiente', @AMBIENTEMAESTRA) > 0
	BEGIN
		SET @cant_ambientes = 1;
	    SET @extra = 'SIN EXTRA';
	END
ELSE
	BEGIN
	IF (CHARINDEX('con', @AMBIENTEMAESTRA) > 0 OR CHARINDEX('Con', @AMBIENTEMAESTRA) > 0)
        BEGIN
            SET @cant_ambientes = CAST(SUBSTRING(@AMBIENTEMAESTRA, 1, 1) AS DECIMAL(18, 0));
            SET @extra = 'CON DEPENDENCIA';
		END
    ELSE
		BEGIN
        IF (CHARINDEX('ambientes', @AMBIENTEMAESTRA) > 0 OR CHARINDEX('Ambientes', @AMBIENTEMAESTRA) > 0)
        BEGIN
            SET @cant_ambientes = CAST(SUBSTRING(@AMBIENTEMAESTRA, 1, 1) AS DECIMAL(18, 0));
            SET @extra = 'SIN EXTRA';
			END
		END
	END
SELECT @ambiente = NRO_AMBIENTE
FROM CUATRO_HISPANOS.AMBIENTE AS A
WHERE A.CANTIDAD = @cant_ambientes
AND A.EXTRA = @extra

RETURN @ambiente
END
GO






CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_provincia(@NOMBRE NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS 
BEGIN

DECLARE @id_provincia NUMERIC (18,0)

SELECT @id_provincia = NRO_PROVINCIA 
FROM CUATRO_HISPANOS.PROVINCIA AS P
WHERE P.PROVINCIA = @NOMBRE;

RETURN @id_provincia;
END
GO

CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_localidad(@LOCALIDAD NVARCHAR(100), @PROVINCIA NVARCHAR(100))
RETURNS NUMERIC(18,0)
AS 
BEGIN
DECLARE @id_localidad NUMERIC (18,0)

DECLARE @id_provincia NUMERIC(18,0)

SET @id_provincia = CUATRO_HISPANOS.obtener_ID_provincia(@PROVINCIA)

SELECT @id_localidad = NRO_LOCALIDAD
FROM CUATRO_HISPANOS.LOCALIDAD AS L
WHERE L.LOCALIDAD = @LOCALIDAD
AND L.NRO_PROVINCIA = @id_provincia;


RETURN @id_localidad;
END
GO


CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_barrio(@BARRIO NVARCHAR(100), @LOCALIDAD NVARCHAR(100), @PROVINCIA NVARCHAR(100))
RETURNS NUMERIC(18,0)
AS 
BEGIN


DECLARE @id_localidad NUMERIC(18,0)
SET @id_localidad = CUATRO_HISPANOS.obtener_ID_localidad(@LOCALIDAD, @PROVINCIA)


DECLARE @id_barrio NUMERIC (18,0)

SELECT @id_barrio = NRO_BARRIO
FROM CUATRO_HISPANOS.BARRIO AS B
WHERE B.BARRIO = @BARRIO
AND B.NRO_LOCALIDAD = @id_localidad;

RETURN @id_barrio;
END
GO


CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_ubicacion(@BARRIO NVARCHAR(100), @LOCALIDAD NVARCHAR(100), @PROVINCIA NVARCHAR(100) ) RETURNS NUMERIC(18,0) AS
BEGIN

DECLARE @id_provincia NUMERIC(18,0)
SET @id_provincia = CUATRO_HISPANOS.obtener_ID_provincia(@PROVINCIA)
DECLARE @id_localidad NUMERIC(18,0)
SET @id_localidad = CUATRO_HISPANOS.obtener_ID_localidad(@LOCALIDAD, @PROVINCIA)
DECLARE @id_barrio NUMERIC(18,0)
SET @id_barrio = CUATRO_HISPANOS.obtener_ID_barrio(@BARRIO, @LOCALIDAD,@PROVINCIA)


DECLARE @id_ubicacion NUMERIC(18,0)

SELECT @id_ubicacion = NRO_UBICACION
FROM CUATRO_HISPANOS.UBICACION AS U
WHERE U.NRO_BARRIO = @id_barrio
AND U.NRO_LOCALIDAD = @id_localidad
AND U.NRO_PROVINCIA = @id_provincia

RETURN @id_ubicacion;
END
GO







CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_caracteristica(@NOMBRE NVARCHAR(100)) 
RETURNS NUMERIC(18,0) 
AS
BEGIN

DECLARE @id_caracteristica NUMERIC (18,0)

SELECT @id_caracteristica = NRO_CARACTERISTICA
FROM CUATRO_HISPANOS.CARACTERISTICA AS C
WHERE C.TIPO_CARACTERISTICA = @NOMBRE ;
RETURN @id_caracteristica

END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_agente(@DNI NUMERIC(18,0), @APELLIDO NVARCHAR(100), @NOMBRE NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_agente NUMERIC(18,0);

SELECT @id_agente = NRO_AGENTE
FROM CUATRO_HISPANOS.AGENTE as A
WHERE  A.DNI_AGENTE = @DNI 
AND A.APELLIDO_AGENTE = @APELLIDO
AND A.NOMBRE_AGENTE = @NOMBRE;

RETURN @id_agente;
END
GO




CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_operacion(@OPERACION NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_op NUMERIC(18,0);

SELECT @id_op = NRO_OPERACION
FROM CUATRO_HISPANOS.TIPO_OPERACION as O
WHERE  O.TIPO_OPERACION= @OPERACION

RETURN @id_op;
END
GO




CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_estadoAnuncio(@ESTADO NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_estadoAnuncio NUMERIC(18,0);

SELECT @id_estadoAnuncio = NRO_ESTADO_ANUNCIO
FROM CUATRO_HISPANOS.ESTADO_ANUNCIO as E
WHERE  E.ESTADO_ANUNCIO= @ESTADO

RETURN @id_estadoAnuncio;
END
GO





CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_moneda(@MONEDA NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_moneda NUMERIC(18,0);

SELECT @id_moneda = TIPO_MONEDA
FROM CUATRO_HISPANOS.MONEDA as M
WHERE  M.MONEDA= @MONEDA

RETURN @id_moneda;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_periodo(@PERIODO NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_periodo NUMERIC(18,0);

SELECT @id_periodo = NRO_TIPO_PERIODO
FROM CUATRO_HISPANOS.TIPO_PERIODO as P
WHERE  P.TIPO_PERIODO = @PERIODO
RETURN @id_periodo;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_estadoAlquiler(@ESTADO NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_estado NUMERIC(18,0);
SELECT @id_estado = NRO_ESTADO_ALQUILER
FROM CUATRO_HISPANOS.ESTADO_ALQUILER as E
WHERE  E.ESTADO_ALQUILER = @ESTADO

RETURN @id_estado;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_inquilino(@DNI NUMERIC(18,0), @APELLIDO NVARCHAR(100), @NOMBRE NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_inq NUMERIC(18,0);

SELECT @id_inq = NRO_INQUILINO 
FROM CUATRO_HISPANOS.INQUILINO as I
WHERE  I.INQUILINO_DNI = @DNI 
AND I.INQUILINO_APELLIDO = @APELLIDO
AND I.INQUILINO_NOMBRE = @NOMBRE;

RETURN @id_inq;
END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_medioPago(@MEDIO_PAGO NVARCHAR(100))
RETURNS NUMERIC (18,0)
AS
BEGIN

DECLARE @id_medio_pago NUMERIC (18,0);

SELECT @id_medio_pago = NRO_MEDIO_PAGO
FROM CUATRO_HISPANOS.MEDIO_PAGO AS M
WHERE M.MEDIO_PAGO = @MEDIO_PAGO
RETURN @id_medio_pago;

END
GO



CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_comprador(@DNI NUMERIC(18,0), @APELLIDO NVARCHAR(100), @NOMBRE NVARCHAR(100)) 
RETURNS NUMERIC(18,0)
AS
BEGIN

DECLARE @id_comprador NUMERIC(18,0);

SELECT @id_comprador = NRO_COMPRADOR
FROM CUATRO_HISPANOS.COMPRADOR as C
WHERE  C.COMPRADOR_DNI = @DNI 
AND C.COMPRADOR_APELLIDO = @APELLIDO
AND C.COMPRADOR_NOMBRE = @NOMBRE;

RETURN @id_comprador;
END
GO

CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_Anuncio(@CODIGO NUMERIC(19,0))
RETURNS NUMERIC (19,0)
AS
BEGIN

DECLARE @id_anuncio NUMERIC (19,0);

SELECT @id_anuncio = NRO_ANUNCIO
FROM CUATRO_HISPANOS.ANUNCIO AS A
WHERE A.ANUNCIO_CODIGO = @CODIGO
RETURN @id_anuncio

END
GO

CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_Alquiler(@CODIGO NUMERIC(18,0))
RETURNS NUMERIC (18,0)
AS
BEGIN

DECLARE @id_alquiler NUMERIC(18,0);

SELECT @id_alquiler = NRO_ALQUILER
FROM CUATRO_HISPANOS.ALQUILER AS A
WHERE A.ALQUILER_CODIGO = @CODIGO
RETURN @id_alquiler

END
GO


CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_venta(@CODIGO NUMERIC(18,0))
RETURNS NUMERIC (18,0)
AS
BEGIN

DECLARE @id_venta NUMERIC(18,0);

SELECT @id_venta = NRO_VENTA
FROM CUATRO_HISPANOS.VENTA AS A
WHERE A.CODIGO_VENTA = @CODIGO
RETURN @id_venta

END
GO

CREATE FUNCTION CUATRO_HISPANOS.obtener_ID_inmueble(@CODIGO NUMERIC(18,0))
RETURNS NUMERIC (18,0)
AS
BEGIN

DECLARE @id_INMUEBLE NUMERIC(18,0);

SELECT @id_INMUEBLE  = NRO_INMUEBLE
FROM CUATRO_HISPANOS.INMUEBLE AS A
WHERE A.INMUEBLE_CODIGO = @CODIGO
RETURN @id_INMUEBLE 

END
GO





--STORED PROCEDURES PARA LA MIGRACION DE MAESTRA A NUESTRO ESQUEMA

CREATE PROCEDURE CUATRO_HISPANOS.migrar_ambiente AS
BEGIN

DECLARE @ambiente NVARCHAR(100);
DECLARE @cant_ambientes NUMERIC(18,0);
DECLARE @extra NVARCHAR(100);


DECLARE cursorAmbiente CURSOR LOCAL FOR 
SELECT DISTINCT INMUEBLE_CANT_AMBIENTES
FROM gd_esquema.Maestra
WHERE INMUEBLE_CANT_AMBIENTES IS NOT NULL;


OPEN cursorAmbiente;


FETCH NEXT FROM cursorAmbiente INTO @ambiente;

WHILE @@FETCH_STATUS = 0
BEGIN
    IF CHARINDEX('Monoambiente', @ambiente) > 0
    BEGIN
        SET @cant_ambientes = 1;
        SET @extra = 'SIN EXTRA';
    END
    ELSE
    BEGIN

        IF (CHARINDEX('con', @ambiente) > 0 OR CHARINDEX('Con', @ambiente) > 0)
        BEGIN
            SET @cant_ambientes = CAST(SUBSTRING(@ambiente, 1, 1) AS DECIMAL(18, 0));
            SET @extra = 'CON DEPENDENCIA';
        END

        ELSE

        BEGIN

        IF (CHARINDEX('ambientes', @ambiente) > 0 OR CHARINDEX('Ambientes', @ambiente) > 0)
        BEGIN
            SET @cant_ambientes = CAST(SUBSTRING(@ambiente, 1, 1) AS DECIMAL(18, 0));
            SET @extra = 'SIN EXTRA';
        END

        END

    END


    INSERT INTO CUATRO_HISPANOS.AMBIENTE (EXTRA, CANTIDAD)
    VALUES (@extra, @cant_ambientes);

    FETCH NEXT FROM cursorAmbiente INTO @ambiente;
END;


CLOSE cursorAmbiente;
DEALLOCATE cursorAmbiente;

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_provincia AS 
BEGIN 



INSERT INTO CUATRO_HISPANOS.PROVINCIA(PROVINCIA)
SELECT DISTINCT INMUEBLE_PROVINCIA
FROM gd_esquema.Maestra WHERE INMUEBLE_PROVINCIA IS NOT NULL


END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_localidad AS 
BEGIN 


SELECT DISTINCT INMUEBLE_LOCALIDAD, INMUEBLE_PROVINCIA INTO #aux_localidad
FROM gd_esquema.Maestra WHERE INMUEBLE_LOCALIDAD IS NOT NULL

INSERT INTO CUATRO_HISPANOS.LOCALIDAD (LOCALIDAD, NRO_PROVINCIA)
SELECT INMUEBLE_LOCALIDAD, CUATRO_HISPANOS.obtener_ID_provincia(INMUEBLE_PROVINCIA)
FROM #aux_localidad

DROP TABLE #aux_localidad


END
GO

CREATE PROCEDURE CUATRO_HISPANOS.migrar_localidad_sucursal AS
BEGIN

SELECT DISTINCT SUCURSAL_LOCALIDAD, SUCURSAL_PROVINCIA INTO #aux_localidad
FROM gd_esquema.Maestra WHERE SUCURSAL_LOCALIDAD IS NOT NULL

INSERT INTO CUATRO_HISPANOS.LOCALIDAD (LOCALIDAD, NRO_PROVINCIA)
SELECT SUCURSAL_LOCALIDAD, CUATRO_HISPANOS.obtener_ID_provincia(SUCURSAL_PROVINCIA)
FROM #aux_localidad
DROP TABLE #aux_localidad

END
GO


CREATE PROCEDURE CUATRO_HISPANOS.migrar_barrio AS 
BEGIN 

SELECT DISTINCT INMUEBLE_BARRIO, INMUEBLE_LOCALIDAD, INMUEBLE_PROVINCIA INTO
#aux_barrio
FROM gd_esquema.Maestra WHERE INMUEBLE_BARRIO IS NOT NULL

INSERT INTO CUATRO_HISPANOS.BARRIO (BARRIO, NRO_LOCALIDAD)
SELECT INMUEBLE_BARRIO, CUATRO_HISPANOS.obtener_ID_localidad(INMUEBLE_LOCALIDAD, INMUEBLE_PROVINCIA)

FROM #aux_barrio

DROP TABLE #aux_barrio

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_ubicacion AS 
BEGIN 


SELECT DISTINCT INMUEBLE_BARRIO, INMUEBLE_LOCALIDAD, INMUEBLE_PROVINCIA
INTO #aux_ubicacion
FROM gd_esquema.Maestra WHERE INMUEBLE_BARRIO IS NOT NULL AND INMUEBLE_LOCALIDAD IS NOT NULL AND INMUEBLE_PROVINCIA IS NOT NULL


INSERT INTO CUATRO_HISPANOS.UBICACION(NRO_BARRIO, NRO_LOCALIDAD, NRO_PROVINCIA)
SELECT CUATRO_HISPANOS.obtener_ID_barrio(INMUEBLE_BARRIO,INMUEBLE_LOCALIDAD, INMUEBLE_PROVINCIA),
CUATRO_HISPANOS.obtener_ID_localidad(INMUEBLE_LOCALIDAD, INMUEBLE_PROVINCIA),
CUATRO_HISPANOS.obtener_ID_provincia(INMUEBLE_PROVINCIA)
FROM #aux_ubicacion

DROP TABLE #aux_ubicacion

END
GO

CREATE PROCEDURE CUATRO_HISPANOS.migrar_propietario AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.PROPIETARIO
(PROPIETARIO_NOMBRE,
PROPIETARIO_APELLIDO,
PROPIETARIO_DNI,
PROPIETARIO_FECHA_REGISTRO,
PROPIETARIO_TELEFONO,
PROPIETARIO_MAIL,
PROPIETARIO_FECHA_NAC)
SELECT DISTINCT PROPIETARIO_NOMBRE,
 PROPIETARIO_APELLIDO,
 PROPIETARIO_DNI,
 PROPIETARIO_FECHA_REGISTRO, 
PROPIETARIO_TELEFONO,
PROPIETARIO_MAIL, 
PROPIETARIO_FECHA_NAC
FROM gd_esquema.Maestra WHERE PROPIETARIO_APELLIDO IS NOT NULL AND PROPIETARIO_DNI IS NOT NULL AND PROPIETARIO_NOMBRE IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.agregar_Caracteristica(@NOMBRE NVARCHAR(100))
AS 
BEGIN

INSERT INTO CUATRO_HISPANOS.CARACTERISTICA (TIPO_CARACTERISTICA) VALUES
(@NOMBRE);

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_caracteristica 
AS 
BEGIN 

EXEC CUATRO_HISPANOS.agregar_Caracteristica @NOMBRE ='WIFI'
EXEC CUATRO_HISPANOS.agregar_Caracteristica @NOMBRE ='CABLE'
EXEC CUATRO_HISPANOS.agregar_Caracteristica @NOMBRE ='CALEFACCION'
EXEC CUATRO_HISPANOS.agregar_Caracteristica @NOMBRE ='GAS'

END
GO

CREATE PROCEDURE CUATRO_HISPANOS.migrar_caracteristica_X_inmueble
AS
BEGIN

SELECT INMUEBLE_CODIGO, 
		INMUEBLE_CARACTERISTICA_WIFI, 
		INMUEBLE_CARACTERISTICA_CABLE, 
		INMUEBLE_CARACTERISTICA_CALEFACCION,
		INMUEBLE_CARACTERISTICA_GAS
INTO #aux
FROM gd_esquema.Maestra 
WHERE INMUEBLE_CODIGO IS NOT NULL 

INSERT INTO CUATRO_HISPANOS.CARACTERISTICA_X_INMUEBLE (NRO_INMUEBLE, NRO_CARACTERISTICA )
SELECT DISTINCT CUATRO_HISPANOS.obtener_ID_inmueble(INMUEBLE_CODIGO) , CUATRO_HISPANOS.obtener_id_caracteristica('WIFI')
FROM #aux 
WHERE INMUEBLE_CODIGO IS NOT NULL AND INMUEBLE_CARACTERISTICA_WIFI IS NOT NULL 
AND INMUEBLE_CARACTERISTICA_WIFI = 1

INSERT INTO CUATRO_HISPANOS.CARACTERISTICA_X_INMUEBLE (NRO_INMUEBLE, NRO_CARACTERISTICA)
SELECT DISTINCT CUATRO_HISPANOS.obtener_ID_inmueble(INMUEBLE_CODIGO) , CUATRO_HISPANOS.obtener_id_caracteristica('CABLE')
FROM #aux 
WHERE INMUEBLE_CODIGO IS NOT NULL AND INMUEBLE_CARACTERISTICA_CABLE IS NOT NULL  AND INMUEBLE_CARACTERISTICA_CABLE = 1

INSERT INTO CUATRO_HISPANOS.CARACTERISTICA_X_INMUEBLE (NRO_INMUEBLE, NRO_CARACTERISTICA)
SELECT DISTINCT CUATRO_HISPANOS.obtener_ID_inmueble(INMUEBLE_CODIGO) , CUATRO_HISPANOS.obtener_id_caracteristica('CALEFACCION')
FROM #aux
WHERE INMUEBLE_CODIGO IS NOT NULL AND INMUEBLE_CARACTERISTICA_CALEFACCION IS NOT NULL  AND INMUEBLE_CARACTERISTICA_CALEFACCION = 1

INSERT INTO CUATRO_HISPANOS.CARACTERISTICA_X_INMUEBLE (NRO_INMUEBLE, NRO_CARACTERISTICA)
SELECT DISTINCT CUATRO_HISPANOS.obtener_ID_inmueble(INMUEBLE_CODIGO)   , CUATRO_HISPANOS.obtener_id_caracteristica('GAS')
FROM #aux 
WHERE INMUEBLE_CODIGO IS NOT NULL AND INMUEBLE_CARACTERISTICA_GAS IS NOT NULL  AND INMUEBLE_CARACTERISTICA_GAS = 1

DROP TABLE #aux

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_estadoAnuncio AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.ESTADO_ANUNCIO(ESTADO_ANUNCIO)
SELECT DISTINCT ANUNCIO_ESTADO
FROM gd_esquema.Maestra WHERE ANUNCIO_ESTADO IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_tipo_operacion AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.TIPO_OPERACION(TIPO_OPERACION )
SELECT DISTINCT ANUNCIO_TIPO_OPERACION
FROM gd_esquema.Maestra WHERE ANUNCIO_TIPO_OPERACION IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_tipoPeriodo AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.TIPO_PERIODO(TIPO_PERIODO)
SELECT DISTINCT ANUNCIO_TIPO_PERIODO
FROM gd_esquema.Maestra WHERE ANUNCIO_TIPO_PERIODO IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_estadoInmueble AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.ESTADO_INMUEBLE(ESTADO_INMUEBLE)
SELECT DISTINCT INMUEBLE_ESTADO
FROM gd_esquema.Maestra WHERE INMUEBLE_ESTADO IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_orientacion AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.ORIENTACION(ORIENTACION)
SELECT DISTINCT INMUEBLE_ORIENTACION
FROM gd_esquema.Maestra WHERE INMUEBLE_ORIENTACION IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_disposicion AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.DISPOSICION(DISPOSICION)
SELECT DISTINCT INMUEBLE_DISPOSICION
FROM gd_esquema.Maestra WHERE INMUEBLE_DISPOSICION IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_tipoInmueble AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.TIPO_INMUEBLE(TIPO_INMUEBLE)
SELECT DISTINCT INMUEBLE_TIPO_INMUEBLE
FROM gd_esquema.Maestra WHERE INMUEBLE_TIPO_INMUEBLE IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_moneda AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.MONEDA(MONEDA)
SELECT DISTINCT ANUNCIO_MONEDA
FROM gd_esquema.Maestra WHERE ANUNCIO_MONEDA IS NOT NULL

INSERT INTO CUATRO_HISPANOS.MONEDA(MONEDA)
SELECT DISTINCT PAGO_VENTA_MONEDA FROM gd_esquema.Maestra WHERE PAGO_VENTA_MONEDA NOT IN (SELECT CUATRO_HISPANOS.MONEDA.MONEDA FROM CUATRO_HISPANOS.MONEDA ) AND PAGO_VENTA_MONEDA IS NOT NULL
 
INSERT INTO CUATRO_HISPANOS.MONEDA(MONEDA)
SELECT DISTINCT VENTA_MONEDA FROM gd_esquema.Maestra WHERE VENTA_MONEDA NOT IN (SELECT CUATRO_HISPANOS.MONEDA.MONEDA FROM CUATRO_HISPANOS.MONEDA ) AND VENTA_MONEDA IS NOT NULL

END
GO


CREATE PROCEDURE CUATRO_HISPANOS.migrar_estadoAlquiler AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.ESTADO_ALQUILER(ESTADO_ALQUILER)
SELECT DISTINCT ALQUILER_ESTADO
FROM gd_esquema.Maestra WHERE ALQUILER_ESTADO IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_medio_pago AS 
BEGIN 

INSERT INTO CUATRO_HISPANOS.MEDIO_PAGO(MEDIO_PAGO)
SELECT DISTINCT PAGO_VENTA_MEDIO_PAGO
FROM gd_esquema.Maestra WHERE PAGO_VENTA_MEDIO_PAGO IS NOT NULL

INSERT INTO CUATRO_HISPANOS.MEDIO_PAGO(MEDIO_PAGO)
SELECT DISTINCT PAGO_ALQUILER_MEDIO_PAGO
FROM gd_esquema.Maestra WHERE PAGO_ALQUILER_MEDIO_PAGO IS NOT NULL 
AND PAGO_ALQUILER_MEDIO_PAGO NOT IN (SELECT CUATRO_HISPANOS.MEDIO_PAGO.MEDIO_PAGO 
                                     FROM CUATRO_HISPANOS.MEDIO_PAGO )

END
GO


CREATE PROCEDURE CUATRO_HISPANOS.migrar_sucursal AS
BEGIN


SELECT DISTINCT 
SUCURSAL_CODIGO,
SUCURSAL_NOMBRE,
SUCURSAL_DIRECCION,
SUCURSAL_TELEFONO,
SUCURSAL_LOCALIDAD,
SUCURSAL_PROVINCIA
INTO #aux_sucursal
FROM gd_esquema.Maestra WHERE SUCURSAL_CODIGO IS NOT NULL


INSERT INTO CUATRO_HISPANOS.SUCURSAL 
(NRO_SUCURSAL,
 SUCURSAL_NOMBRE,
 SUCURSAL_DIRECCION,
 SUCURSAL_TELEFONO,
 NRO_LOCALIDAD
)
	
SELECT 
SUCURSAL_CODIGO,
SUCURSAL_NOMBRE,
SUCURSAL_DIRECCION,
SUCURSAL_TELEFONO,
CUATRO_HISPANOS.obtener_ID_localidad(SUCURSAL_LOCALIDAD, SUCURSAL_PROVINCIA)
FROM #aux_sucursal

DROP TABLE #aux_sucursal


END
GO



CREATE OR ALTER PROCEDURE CUATRO_HISPANOS.migrar_agente AS
BEGIN

INSERT INTO CUATRO_HISPANOS.AGENTE ( 
    NRO_SUCURSAL, 
    NOMBRE_AGENTE ,
    APELLIDO_AGENTE,
    DNI_AGENTE ,
    FECHA_REGISTRO_AGENTE ,
    TELEFONO ,
    MAIL ,
    FECHA_NAC_AGENTE 
)
	
SELECT DISTINCT SUCURSAL_CODIGO,
	   AGENTE_NOMBRE,
	  AGENTE_APELLIDO,
	  AGENTE_DNI,
	  AGENTE_FECHA_REGISTRO,
 	 AGENTE_TELEFONO,
	 AGENTE_MAIL,
	 AGENTE_FECHA_NAC
FROM gd_esquema.Maestra 
WHERE AGENTE_NOMBRE IS NOT NULL 
AND AGENTE_APELLIDO IS NOT NULL 
AND AGENTE_DNI IS NOT NULL
AND SUCURSAL_CODIGO IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_inquilino AS
BEGIN

INSERT INTO CUATRO_HISPANOS.INQUILINO (
INQUILINO_NOMBRE, 
INQUILINO_APELLIDO, 
INQUILINO_DNI,
INQUILINO_FECHA_REGISTRO,
INQUILINO_TELEFONO,
INQUILINO_MAIL,
INQUILINO_FECHA_NAC
)
	
SELECT DISTINCT
INQUILINO_NOMBRE,
INQUILINO_APELLIDO,
INQUILINO_DNI,
INQUILINO_FECHA_REGISTRO,
INQUILINO_TELEFONO,
INQUILINO_MAIL,
INQUILINO_FECHA_NAC
FROM gd_esquema.Maestra WHERE INQUILINO_NOMBRE IS NOT NULL AND INQUILINO_APELLIDO IS NOT NULL AND INQUILINO_DNI IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_detalleImporte AS
BEGIN

SELECT DISTINCT 
DETALLE_ALQ_NRO_PERIODO_INI,
DETALLE_ALQ_NRO_PERIODO_FIN,
DETALLE_ALQ_PRECIO,
ALQUILER_CODIGO
INTO #aux
FROM gd_esquema.Maestra 
WHERE ALQUILER_CODIGO IS NOT NULL 
AND DETALLE_ALQ_NRO_PERIODO_INI IS NOT NULL 
AND DETALLE_ALQ_NRO_PERIODO_FIN IS NOT NULL 
AND DETALLE_ALQ_PRECIO IS NOT NULL



INSERT INTO CUATRO_HISPANOS.DETALLE_IMPORTE
(DETALLE_PERIODO_INICIO,
DETALLE_PERIODO_FIN,
DETALLE_PRECIO,
NRO_ALQUILER)
SELECT DISTINCT DETALLE_ALQ_NRO_PERIODO_INI,
DETALLE_ALQ_NRO_PERIODO_FIN,
DETALLE_ALQ_PRECIO,
CUATRO_HISPANOS.obtener_ID_Alquiler(ALQUILER_CODIGO)
FROM #aux

DROP TABLE #aux

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_pagoAlquiler AS
BEGIN


SELECT DISTINCT
PAGO_ALQUILER_CODIGO,
PAGO_ALQUILER_FECHA,
PAGO_ALQUILER_NRO_PERIODO,
PAGO_ALQUILER_DESC,
PAGO_ALQUILER_FEC_INI,
PAGO_ALQUILER_FEC_FIN,
PAGO_ALQUILER_FECHA_VENCIMIENTO,
PAGO_ALQUILER_IMPORTE,
ALQUILER_CODIGO,
PAGO_ALQUILER_MEDIO_PAGO
INTO #aux_pago_alquiler
FROM gd_esquema.Maestra WHERE PAGO_ALQUILER_CODIGO IS NOT NULL
AND ALQUILER_CODIGO IS NOT NULL
AND PAGO_ALQUILER_FECHA_VENCIMIENTO IS NOT NULL
AND PAGO_ALQUILER_FECHA IS NOT NULL
AND PAGO_ALQUILER_IMPORTE IS NOT NULL


INSERT INTO CUATRO_HISPANOS.PAGO_ALQUILER
(NRO_PAGO_ALQUILER,
FECHA,
NRO_PERIODO,
DESCRIPCION_PERIODO,
FECHA_INICIO_PAGO_ALQUILER,
FECHA_FIN_PAGO_ALQUILER,
FECHA_VENC_ALQUILER,
IMPORTE_PAGO_ALQUILER,
NRO_ALQUILER,
NRO_MEDIO_PAGO)

SELECT 
PAGO_ALQUILER_CODIGO,
PAGO_ALQUILER_FECHA,
PAGO_ALQUILER_NRO_PERIODO,
PAGO_ALQUILER_DESC,
PAGO_ALQUILER_FEC_INI,
PAGO_ALQUILER_FEC_FIN,
PAGO_ALQUILER_FECHA_VENCIMIENTO,
PAGO_ALQUILER_IMPORTE,
CUATRO_HISPANOS.obtener_ID_Alquiler(ALQUILER_CODIGO),
CUATRO_HISPANOS.obtener_ID_medioPago(PAGO_ALQUILER_MEDIO_PAGO)
FROM #aux_pago_alquiler
DROP TABLE #aux_pago_alquiler

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_pagoVenta AS
BEGIN

SELECT DISTINCT
VENTA_CODIGO,
PAGO_VENTA_MEDIO_PAGO,
PAGO_VENTA_MONEDA,
PAGO_VENTA_IMPORTE,
PAGO_VENTA_COTIZACION
INTO #aux_pago_alquiler
FROM gd_esquema.Maestra WHERE VENTA_CODIGO IS NOT NULL


INSERT INTO CUATRO_HISPANOS.PAGO_VENTA
(NRO_VENTA,
MEDIO_PAGO,
MONEDA,
IMPORTE_PAGO_VENTA,
COTIZACION_PAGO_VENTA)

SELECT
CUATRO_HISPANOS.obtener_ID_venta(VENTA_CODIGO),
CUATRO_HISPANOS.obtener_ID_medioPago(PAGO_VENTA_MEDIO_PAGO),
CUATRO_HISPANOS.obtener_ID_moneda(PAGO_VENTA_MONEDA),
PAGO_VENTA_IMPORTE,
PAGO_VENTA_COTIZACION
FROM #aux_pago_alquiler
DROP TABLE #aux_pago_alquiler

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_comprador AS
BEGIN

INSERT INTO CUATRO_HISPANOS.COMPRADOR
(COMPRADOR_NOMBRE,
COMPRADOR_APELLIDO,
COMPRADOR_DNI,
COMPRADOR_FECHA_REGISTRO,
COMPRADOR_TELEFONO,
COMPRADOR_FECHA_NAC,
COMPRADOR_MAIL)

SELECT DISTINCT
COMPRADOR_NOMBRE,
COMPRADOR_APELLIDO,
COMPRADOR_DNI,
COMPRADOR_FECHA_REGISTRO,
COMPRADOR_TELEFONO,
COMPRADOR_FECHA_NAC,
COMPRADOR_MAIL
FROM gd_esquema.Maestra WHERE COMPRADOR_NOMBRE IS NOT NULL AND COMPRADOR_APELLIDO IS NOT NULL AND COMPRADOR_DNI IS NOT NULL

END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_venta AS
BEGIN


SELECT DISTINCT
VENTA_CODIGO,
COMPRADOR_NOMBRE,
COMPRADOR_APELLIDO,
COMPRADOR_DNI,
VENTA_MONEDA,
ANUNCIO_CODIGO,
VENTA_PRECIO_VENTA,
VENTA_COMISION,
VENTA_FECHA
INTO #aux_venta
FROM gd_esquema.Maestra WHERE VENTA_CODIGO IS NOT NULL
AND ANUNCIO_CODIGO IS NOT NULL
AND VENTA_COMISION IS NOT NULL
AND VENTA_FECHA IS NOT NULL
AND VENTA_PRECIO_VENTA IS NOT NULL
AND VENTA_MONEDA IS NOT NULL



INSERT INTO CUATRO_HISPANOS.VENTA
(NRO_COMPRADOR,
MONEDA,
NRO_ANUNCIO,
PRECIO_VENTA,
COMISION_VENTA,
FECHA_VENTA,
CODIGO_VENTA)

SELECT
CUATRO_HISPANOS.obtener_ID_comprador(COMPRADOR_DNI, COMPRADOR_APELLIDO, COMPRADOR_NOMBRE),
CUATRO_HISPANOS.obtener_ID_moneda(VENTA_MONEDA),
CUATRO_HISPANOS.obtener_ID_Anuncio(ANUNCIO_CODIGO),
VENTA_PRECIO_VENTA,
VENTA_COMISION,
VENTA_FECHA,
VENTA_CODIGO
FROM #aux_venta
DROP TABLE #aux_venta 


END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_inmueble AS
BEGIN


SELECT DISTINCT 
PROPIETARIO_DNI, 
PROPIETARIO_APELLIDO, 
PROPIETARIO_NOMBRE, 
INMUEBLE_ORIENTACION, 
INMUEBLE_DISPOSICION, 
INMUEBLE_TIPO_INMUEBLE, 
INMUEBLE_ESTADO, 
INMUEBLE_CODIGO, 
INMUEBLE_NOMBRE, 
INMUEBLE_DESCRIPCION, 
INMUEBLE_DIRECCION, 
INMUEBLE_ANTIGUEDAD, 
INMUEBLE_SUPERFICIETOTAL, 
INMUEBLE_EXPESAS,
INMUEBLE_CANT_AMBIENTES,
INMUEBLE_BARRIO,
INMUEBLE_LOCALIDAD,
INMUEBLE_PROVINCIA
INTO #aux_inmueble
FROM gd_esquema.Maestra WHERE INMUEBLE_CODIGO IS NOT NULL
AND INMUEBLE_SUPERFICIETOTAL IS NOT NULL
AND INMUEBLE_TIPO_INMUEBLE IS NOT NULL

INSERT INTO CUATRO_HISPANOS.INMUEBLE 
(NOMBRE_INMUEBLE,
DESCRIPCION_INMUEBLE,
DIRECCION_INMUEBLE,
ANTIGUEDAD_INMUEBLE,
SUPERFICIE_INMUEBLE,
EXPENSAS_INMUEBLE,
NRO_PROPIETARIO, 
NRO_ORIENTACION, 
NRO_DISPOSICION, 
NRO_TIPO, 
NRO_ESTADO,
NRO_AMBIENTE,
NRO_UBICACION,
INMUEBLE_CODIGO
)
	
SELECT 
INMUEBLE_NOMBRE,
INMUEBLE_DESCRIPCION,
INMUEBLE_DIRECCION,
INMUEBLE_ANTIGUEDAD,
INMUEBLE_SUPERFICIETOTAL,
INMUEBLE_EXPESAS,
CUATRO_HISPANOS.obtener_ID_prop(PROPIETARIO_DNI, PROPIETARIO_APELLIDO, PROPIETARIO_NOMBRE),
CUATRO_HISPANOS.obtener_ID_orientacion(INMUEBLE_ORIENTACION) ,
CUATRO_HISPANOS.obtener_ID_disposicion(INMUEBLE_DISPOSICION) , 
CUATRO_HISPANOS.obtener_ID_tipo_inmueble(INMUEBLE_TIPO_INMUEBLE) , 
CUATRO_HISPANOS.obtener_ID_estadoInmueble(INMUEBLE_ESTADO),
CUATRO_HISPANOS.obtener_ID_ambiente(INMUEBLE_CANT_AMBIENTES),
CUATRO_HISPANOS.obtener_ID_ubicacion(INMUEBLE_BARRIO, INMUEBLE_LOCALIDAD, INMUEBLE_PROVINCIA),
INMUEBLE_CODIGO

FROM #aux_inmueble

DROP TABLE #aux_inmueble


END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_anuncio AS
BEGIN

SELECT DISTINCT 
ANUNCIO_FECHA_PUBLICACION,
ANUNCIO_FECHA_FINALIZACION,
ANUNCIO_PRECIO_PUBLICADO,
ANUNCIO_COSTO_ANUNCIO,
ANUNCIO_TIPO_OPERACION,
ANUNCIO_ESTADO,
ANUNCIO_TIPO_PERIODO,
AGENTE_NOMBRE,
AGENTE_APELLIDO,
AGENTE_DNI,
INMUEBLE_CODIGO,
ANUNCIO_MONEDA,
ANUNCIO_CODIGO
INTO #aux_anuncio
FROM gd_esquema.Maestra 
WHERE ANUNCIO_CODIGO IS NOT NULL
AND INMUEBLE_CODIGO IS NOT NULL
AND ANUNCIO_TIPO_OPERACION IS NOT NULL
AND ANUNCIO_PRECIO_PUBLICADO IS NOT NULL
AND ANUNCIO_MONEDA IS NOT NULL

INSERT INTO CUATRO_HISPANOS.ANUNCIO 
(    FECHA_PUBLICACION_ANUNCIO,
    FECHA_FINALIZACION_ANUNCIO,
    PRECIO_ANUNCIO,
    COSTO_ANUNCIO,
    NRO_AGENTE,
    NRO_INMUEBLE,
    NRO_OPERACION,
    NRO_ESTADO_ANUNCIO,
    NRO_TIPO_PERIODO,
    TIPO_MONEDA,
	ANUNCIO_CODIGO
)
	
SELECT ANUNCIO_FECHA_PUBLICACION,
ANUNCIO_FECHA_FINALIZACION,
ANUNCIO_PRECIO_PUBLICADO,
ANUNCIO_COSTO_ANUNCIO,
CUATRO_HISPANOS.obtener_ID_agente(AGENTE_DNI, AGENTE_APELLIDO, AGENTE_NOMBRE),
CUATRO_HISPANOS.obtener_ID_inmueble(INMUEBLE_CODIGO),
CUATRO_HISPANOS.obtener_ID_operacion(ANUNCIO_TIPO_OPERACION),
CUATRO_HISPANOS.obtener_ID_estadoAnuncio(ANUNCIO_ESTADO),
CUATRO_HISPANOS.obtener_ID_periodo(ANUNCIO_TIPO_PERIODO),
CUATRO_HISPANOS.obtener_ID_moneda(ANUNCIO_MONEDA),
ANUNCIO_CODIGO

FROM #aux_anuncio

DROP TABLE #aux_anuncio
END
GO



CREATE PROCEDURE CUATRO_HISPANOS.migrar_Alquiler AS
BEGIN

SELECT DISTINCT ALQUILER_CODIGO,
ALQUILER_FECHA_INICIO,
ALQUILER_FECHA_FIN,
ALQUILER_CANT_PERIODOS,
ALQUILER_DEPOSITO,
ALQUILER_COMISION,
ALQUILER_GASTOS_AVERIGUA,
INQUILINO_NOMBRE,
INQUILINO_APELLIDO,
INQUILINO_DNI,
ANUNCIO_CODIGO,
ALQUILER_ESTADO
INTO #aux_alquiler
FROM gd_esquema.Maestra WHERE ALQUILER_CODIGO IS NOT NULL
AND ANUNCIO_CODIGO IS NOT NULL
AND ALQUILER_ESTADO IS NOT NULL
AND ALQUILER_COMISION IS NOT NULL

INSERT INTO CUATRO_HISPANOS.ALQUILER ( 
FECHA_INICIO,
FECHA_FIN,
CANT_PERIODOS,
DEPOSITO_ALQUILER,
COMISION_ALQUILER,
GASTOS_AVERIGUACIONES,
NRO_INQUILINO,
NRO_ANUNCIO,
NRO_ESTADO_ALQUILER,
ALQUILER_CODIGO)
	
SELECT  
ALQUILER_FECHA_INICIO,
ALQUILER_FECHA_FIN,
ALQUILER_CANT_PERIODOS,
ALQUILER_DEPOSITO,
ALQUILER_COMISION,
ALQUILER_GASTOS_AVERIGUA,
CUATRO_HISPANOS.obtener_ID_inquilino(INQUILINO_DNI, INQUILINO_APELLIDO, INQUILINO_NOMBRE),
CUATRO_HISPANOS.obtener_ID_Anuncio(ANUNCIO_CODIGO),
CUATRO_HISPANOS.obtener_ID_estadoAlquiler(ALQUILER_ESTADO),
ALQUILER_CODIGO
FROM #aux_alquiler
DROP TABLE #aux_alquiler

END
GO

--INDICES PARA FACILITAR BUSQUEDA


CREATE INDEX indice_propietario ON CUATRO_HISPANOS.PROPIETARIO (PROPIETARIO_DNI, PROPIETARIO_APELLIDO);				

CREATE INDEX indice_comprador ON CUATRO_HISPANOS.COMPRADOR (COMPRADOR_DNI, COMPRADOR_APELLIDO);				

CREATE INDEX indice_inquilino ON CUATRO_HISPANOS.INQUILINO (INQUILINO_DNI, INQUILINO_APELLIDO);				

CREATE INDEX indice_agente ON CUATRO_HISPANOS.AGENTE (DNI_AGENTE, APELLIDO_AGENTE);	

CREATE INDEX indice_inmueble ON CUATRO_HISPANOS.INMUEBLE (DIRECCION_INMUEBLE);	


--EJECUCIONES DE PROCEDURES DE MIGRACION

EXEC CUATRO_HISPANOS.migrar_ambiente
EXEC CUATRO_HISPANOS.migrar_provincia
EXEC CUATRO_HISPANOS.migrar_localidad
EXEC CUATRO_HISPANOS.migrar_localidad_sucursal
EXEC CUATRO_HISPANOS.migrar_barrio
EXEC CUATRO_HISPANOS.migrar_ubicacion
EXEC CUATRO_HISPANOS.migrar_propietario
EXEC CUATRO_HISPANOS.migrar_caracteristica
EXEC CUATRO_HISPANOS.migrar_orientacion
EXEC CUATRO_HISPANOS.migrar_disposicion
EXEC CUATRO_HISPANOS.migrar_tipoInmueble
EXEC CUATRO_HISPANOS.migrar_estadoInmueble
EXEC CUATRO_HISPANOS.migrar_inmueble
EXEC CUATRO_HISPANOS.migrar_caracteristica_X_inmueble
EXEC CUATRO_HISPANOS.migrar_tipo_operacion
EXEC CUATRO_HISPANOS.migrar_estadoAnuncio
EXEC CUATRO_HISPANOS.migrar_tipoPeriodo
EXEC CUATRO_HISPANOS.migrar_moneda
EXEC CUATRO_HISPANOS.migrar_sucursal
EXEC CUATRO_HISPANOS.migrar_agente
EXEC CUATRO_HISPANOS.migrar_estadoAlquiler
EXEC CUATRO_HISPANOS.migrar_medio_pago
EXEC CUATRO_HISPANOS.migrar_inquilino
EXEC CUATRO_HISPANOS.migrar_comprador
EXEC CUATRO_HISPANOS.migrar_anuncio
EXEC CUATRO_HISPANOS.migrar_Alquiler
EXEC CUATRO_HISPANOS.migrar_detalleImporte 
EXEC CUATRO_HISPANOS.migrar_pagoAlquiler 
EXEC CUATRO_HISPANOS.migrar_venta
EXEC CUATRO_HISPANOS.migrar_pagoVenta
/*
DELETE FROM CUATRO_HISPANOS.venta
DELETE FROM CUATRO_HISPANOS.Alquiler
DELETE FROM CUATRO_HISPANOS.anuncio
DELETE FROM CUATRO_HISPANOS.inmueble
DELETE FROM CUATRO_HISPANOS.ubicacion
DELETE FROM CUATRO_HISPANOS.barrio
DELETE FROM CUATRO_HISPANOS.agente
DELETE FROM CUATRO_HISPANOS.sucursal
DELETE FROM CUATRO_HISPANOS.localidad
DELETE FROM CUATRO_HISPANOS.provincia
DELETE FROM CUATRO_HISPANOS.ambiente
DELETE FROM CUATRO_HISPANOS.propietario
DELETE FROM CUATRO_HISPANOS.caracteristica
DELETE FROM CUATRO_HISPANOS.caracteristica_X_inmueble
DELETE FROM CUATRO_HISPANOS.estado_anuncio
DELETE FROM CUATRO_HISPANOS.tipo_operacion
DELETE FROM CUATRO_HISPANOS.tipo_Periodo
DELETE FROM CUATRO_HISPANOS.estado_Inmueble
DELETE FROM CUATRO_HISPANOS.orientacion
DELETE FROM CUATRO_HISPANOS.disposicion
DELETE FROM CUATRO_HISPANOS.tipo_Inmueble
DELETE FROM CUATRO_HISPANOS.estado_Alquiler
DELETE FROM CUATRO_HISPANOS.medio_pago
DELETE FROM CUATRO_HISPANOS.inquilino
DELETE FROM CUATRO_HISPANOS.moneda
DELETE FROM CUATRO_HISPANOS.comprador
DELETE FROM CUATRO_HISPANOS.pago_Venta
DELETE FROM CUATRO_HISPANOS.detalle_Importe 
DELETE FROM CUATRO_HISPANOS.pago_Alquiler


SELECT * FROM CUATRO_HISPANOS.INQUILINO
order by INQUILINO_APELLIDO*/